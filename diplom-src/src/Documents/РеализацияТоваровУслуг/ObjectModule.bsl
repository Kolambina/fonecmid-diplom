#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Ответственный = Пользователи.ТекущийПользователь();

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");

КонецПроцедуры

//ЛЕА.:Добавлен регистр для записи движений в разрезе номенклатуры и сумм к уплате
//15.11.23
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
    Движения.ОстаткиТоваров.Записывать = Истина;
    Движения.ВКМ_ВыставленныеКОплатеРаботы.Записывать = Истина;
    
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	Для Каждого ТекСтрокаУслуги Из Услуги Цикл
		Движение = Движения.ВКМ_ВыставленныеКОплатеРаботы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Номенклатура = ТекСтрокаУслуги.Номенклатура;
		Движение.Договор = Договор;
		Движение.Период = Дата;
		Движение.Клиент = Контрагент;
		Движение.СуммаКУплате = ТекСтрокаУслуги.Сумма;
	КонецЦикла;

КонецПроцедуры

//ЛЕА

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ЗаказПокупателя.Организация КАК Организация,
				   |	ЗаказПокупателя.Контрагент КАК Контрагент,
				   |	ЗаказПокупателя.Договор КАК Договор,
				   |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
				   |	ЗаказПокупателя.Товары.(
				   |		Ссылка КАК Ссылка,
				   |		НомерСтроки КАК НомерСтроки,
				   |		Номенклатура КАК Номенклатура,
				   |		Количество КАК Количество,
				   |		Цена КАК Цена,
				   |		Сумма КАК Сумма
				   |	) КАК Товары,
				   |	ЗаказПокупателя.Услуги.(
				   |		Ссылка КАК Ссылка,
				   |		НомерСтроки КАК НомерСтроки,
				   |		Номенклатура КАК Номенклатура,
				   |		Количество КАК Количество,
				   |		Цена КАК Цена,
				   |		Сумма КАК Сумма
				   |	) КАК Услуги
				   |ИЗ
				   |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
				   |ГДЕ
				   |	ЗаказПокупателя.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);

	Выборка = Запрос.Выполнить().Выбрать();

	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;

	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;

	Основание = ДанныеЗаполнения;

КонецПроцедуры


//ЛЕА.: Добавили обработчик команды ЗАПОЛНИТЬ (автоматически-ручное заполнение документа по кнопке) проверяем на тип договора и вызываем процедуру из модуля объекта, так же функция используется для обработки ВКМ_МассовоеСозданиеАктов
//20.10.2023
Функция ВыполнитьАвтозаполнение() Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовПриход) КАК Количество,
	|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеПриход) КАК Сумма,
	|	ВКМ_НоменклатураРаботыСпециалиста.Значение КАК Номенклатура,
	|	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеПриход) /
	|		СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.КоличествоЧасовПриход) КАК Цена
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&НачалоПериода, &КонецПериода,,) КАК
	|			ВКМ_ВыполненныеКлиентуРаботыОбороты
	|		ПО РеализацияТоваровУслуг.Организация = ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор.Организация
	|		И РеализацияТоваровУслуг.Контрагент = ВКМ_ВыполненныеКлиентуРаботыОбороты.Клиент
	|		И РеализацияТоваровУслуг.Договор = ВКМ_ВыполненныеКлиентуРаботыОбороты.Договор,
	|	Константа.ВКМ_НоменклатураРаботыСпециалиста КАК ВКМ_НоменклатураРаботыСпециалиста
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И НЕ ВКМ_ВыполненныеКлиентуРаботыОбороты.СуммаКОплатеОборот = НЕОПРЕДЕЛЕНО
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_НоменклатураРаботыСпециалиста.Значение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ДоговорыКонтрагентов.ВКМ_АбоненскаяПлата,
	|	ВКМ_НоменклатураАбонентскаяПлата.Значение,
	|	ДоговорыКонтрагентов.ВКМ_АбоненскаяПлата
	|ИЗ
	|	Константа.ВКМ_НоменклатураАбонентскаяПлата КАК ВКМ_НоменклатураАбонентскаяПлата,
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО РеализацияТоваровУслуг.Договор = ДоговорыКонтрагентов.Ссылка
	|		И РеализацияТоваровУслуг.Организация = ДоговорыКонтрагентов.Организация
	|		И РеализацияТоваровУслуг.Контрагент = ДоговорыКонтрагентов.Владелец
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка
	|	И НЕ ДоговорыКонтрагентов.ВКМ_АбоненскаяПлата = 0";
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НачалоПериода", НАЧАЛОМЕСЯЦА(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КОНЕЦМЕСЯЦА(Дата));
	
	
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Возврат Выборка;


КонецФункции
//ЛЕА


#КонецОбласти

#КонецЕсли